page 53256 "Vendor Assignment Approval"
{
    PageType = List;
    SourceTable = "Vendor Assignment Approval";
    ApplicationArea = All;
    Caption = 'Vendor Assignment Approval List';
    UsageCategory = Lists;
    InsertAllowed = false;
    ModifyAllowed = false;

    layout
    {
        area(content)
        {
            repeater(Group)
            {
                field("ID"; Rec."ID")
                {
                    ApplicationArea = All;
                    Tooltip = 'Unique identifier for the vendor assignment approval record.';
                }
                field("Status"; Rec."Status")
                {
                    ApplicationArea = All;
                    Tooltip = 'Shows the current approval status of the vendor assignment.';
                }
                field("Vendor Assignment ID"; Rec."Vendor Assignment ID")
                {
                    ApplicationArea = All;
                    Tooltip = 'Shows the unique ID of the related vendor assignment.';
                    trigger OnDrillDown()
                    var
                        TenancyContractRec: Record "Vendor Assignment";
                    begin
                        TenancyContractRec.SetRange("Assignment ID", Rec."Vendor Assignment ID");
                        if TenancyContractRec.FindFirst() then
                            PAGE.Run(PAGE::"Vendor Assignment", TenancyContractRec)
                        else
                            Message('The selected Vendor Assignment ID (%1) does not exist in the Vendor Assignment Table.', Rec."Vendor Assignment ID");
                    end;
                }
                field(Remark; Rec.Remark)
                {
                    ApplicationArea = All;
                    Tooltip = 'Remarks or comments related to the vendor assignment approval.';
                }
                field("Vendor ID"; Rec."Vendor ID")
                {
                    ApplicationArea = All;
                    Tooltip = 'Shows the unique ID of the vendor.';
                }
                field("Created By"; Rec."Created By")
                {
                    ApplicationArea = All;
                    Tooltip = 'Specifies the user who created the vendor assignment approval.';
                }
                field("Contract ID"; Rec."Contract ID")
                {
                    ApplicationArea = All;
                    Tooltip = 'Specifies any remarks related to the vendor assignment approval.';
                }
            }
        }
    }
    actions
    {
        area(processing)
        {
            action(Approve)
            {
                Caption = 'Approve';
                ApplicationArea = All;
                Image = Approve;
                Visible = IsPropertyManager;
                Tooltip = 'Approve the selected vendor assignment request with remarks and send notification email.';
                trigger OnAction()
                var
                    SelectedRec: Record "Vendor Assignment Approval";
                    VendorProposalRec: Record "Vendor Assignment";
                    NotificationCodeunit: Codeunit "Vendor Proposal Notification";
                    RemarkDialog: Page "DialogBoxForInvoiceRejection";
                    RemarkText: Text;
                    DialogResult: Action;
                    RecipientEmail: Text;
                begin
                    if Rec.Status = 'Pending' then begin
                        DialogResult := RemarkDialog.RunModal();

                        if DialogResult = Action::OK then begin
                            RemarkText := RemarkDialog.GetReason();

                            if RemarkText <> '' then begin
                                SelectedRec := Rec;
                                SelectedRec.Status := 'Approved';
                                SelectedRec.Remark := CopyStr(RemarkText, 1, StrLen(RemarkText));
                                SelectedRec.Modify();

                                VendorProposalRec.SetRange("Assignment ID", SelectedRec."Vendor Assignment ID");
                                if VendorProposalRec.FindSet() then
                                    repeat
                                        VendorProposalRec."Vendor Assignment Status" := VendorProposalRec."Vendor Assignment Status"::Approved;
                                        VendorProposalRec."Approved By" := GetCurrentUserName();
                                        VendorProposalRec."Reviewed By" := GetCurrentUserName();
                                        VendorProposalRec."Approval Date" := Today;
                                        VendorProposalRec.Remark := CopyStr(RemarkText, 1, StrLen(RemarkText));
                                        VendorProposalRec.Modify();
                                    until VendorProposalRec.Next() = 0;

                                RecipientEmail := NotificationCodeunit.SendAssignmentApprovalEmail(
                                    SelectedRec."Vendor Assignment ID",
                                    SelectedRec."Created By",
                                    RemarkText
                                );

                                Commit();
                                CurrPage.Update();
                                Message('Request Approved Successfully with Remarks and Email Sent to: %1', RecipientEmail);
                            end;
                        end;
                    end else
                        Message('Selected record is not in "Pending" status.');
                end;
            }
            action(Reject)
            {
                Caption = 'Reject';
                ApplicationArea = All;
                Image = Reject;
                Visible = IsPropertyManager;
                Tooltip = 'Reject the selected vendor assignment request with remarks.';

                trigger OnAction()
                var
                    SelectedRec: Record "Vendor Assignment Approval";
                    VendorProposalRec: Record "Vendor Assignment";
                    RemarkDialog: Page "DialogBoxForInvoiceRejection";
                    RemarkText: Text;
                    DialogResult: Action;
                begin
                    if Rec.Status = 'Pending' then begin
                        DialogResult := RemarkDialog.RunModal();

                        if DialogResult = Action::OK then begin
                            RemarkText := RemarkDialog.GetReason();

                            if RemarkText <> '' then begin
                                // Update in approval table
                                SelectedRec := Rec;
                                SelectedRec.Status := 'Declined';
                                SelectedRec.Remark := CopyStr(RemarkText, 1, StrLen(RemarkText));
                                SelectedRec.Modify();

                                // Update in vendor proposal table
                                VendorProposalRec.SetRange("Assignment ID", SelectedRec."Vendor Assignment ID");
                                if VendorProposalRec.FindSet() then
                                    repeat
                                        VendorProposalRec.Remark := CopyStr(RemarkText, 1, StrLen(RemarkText));
                                        VendorProposalRec."Vendor Assignment Status" := VendorProposalRec."Vendor Assignment Status"::Rejected;
                                        VendorProposalRec."Approved By" := GetCurrentUserName();
                                        VendorProposalRec."Reviewed By" := GetCurrentUserName();
                                        VendorProposalRec."Approval Date" := Today;
                                        VendorProposalRec.Modify();
                                    until VendorProposalRec.Next() = 0;

                                Commit();
                                CurrPage.Update();
                                Message('Request Rejected with Remark.');
                            end;
                        end;
                    end else
                        Message('Selected record is not in "Pending" status.');
                end;
            }
        }
        area(navigation)
        {
            action("Open Vendor Assignment")
            {
                Caption = 'Open Vendor Assignment';
                ApplicationArea = All;
                Image = Open;
                Tooltip = 'Open the selected vendor assignment card page.';

                trigger OnAction()
                var
                    TenancyContractRec: Record "Vendor Assignment";
                begin
                    TenancyContractRec.SetRange("Assignment ID", Rec."Vendor Assignment ID");
                    if TenancyContractRec.FindFirst() then
                        PAGE.Run(PAGE::"Vendor Assignment", TenancyContractRec)
                    else
                        Message('The selected Vendor Assignment(%1) does not exist in the Vendor Assignment table.', Rec."Vendor Assignment ID");
                end;
            }


        }
    }

    trigger OnOpenPage()
    begin
        IsPropertyManager := CheckUserRole();
    end;

    procedure CheckUserRole(): Boolean
    var
        UserPersonalization: Record "User Personalization";
    begin
        if UserPersonalization.Get(UserSecurityId()) then
            case UserPersonalization."Profile ID" of
                'PROJECT MANAGER':
                    exit(true);
                else
                    exit(false);
            end;
        exit(false);
    end;

    var
        IsPropertyManager: Boolean;

    local procedure GetCurrentUserName(): Text[100]
    var
        User: Record User;
    begin
        if User.Get(UserSecurityId()) then
            exit(User."Full Name");
        exit(CopyStr(UserId, 1, StrLen(UserId)));
    end;
}

